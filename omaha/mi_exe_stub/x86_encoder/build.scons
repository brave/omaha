# Copyright 2008-2009 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ========================================================================

Import('env')

local_env = env.Clone()
local_env.FilterOut(CPPDEFINES=['_ATL_DEBUG_INTERFACES'])

inputs = [
    'bcj2_encoder.cc',
    'range_encoder.cc'
]

# We need the x86 library for unit tests:

local_env.ComponentLibrary(
    lib_name='bcj2_lib',
    source=inputs,
)

# We need 64 bit binaries to avoid memory-related errors when processing large
# files. Specifically, to avoid error -1073740791 = 0xC0000409, which seems to
# be STATUS_STACK_BUFFER_OVERRUN.

x64_env = local_env.CloneAndMake64Bit()

bcj2_lib = x64_env.ComponentLibrary(
    lib_name='bcj2_lib_64',
    source=inputs,
)

bin_env = x64_env.Clone()
bin_env.FilterOut(LINKFLAGS=['/SUBSYSTEM:WINDOWS'])
bin_env.Append(
    LINKFLAGS=['/SUBSYSTEM:CONSOLE'],
    LIBS=[
        bin_env['atls_libs'][bin_env.Bit('debug')],
        bin_env['crt_libs'][bin_env.Bit('debug')],
        'kernel32.lib',
        'netapi32.lib',
        'psapi.lib',
        'shlwapi.lib',
        'userenv.lib',
        'version.lib',
        'wtsapi32.lib',
        '$LIB_DIR/base_64.lib',
        '$LIB_DIR/lzma_64.lib',
    ],
)

bjc2_env = bin_env.Clone()
bjc2_env.Append(
    LIBS=[ bcj2_lib ],
)
bjc2_env.ComponentProgram(
    prog_name='bcj2',
    source=[
        'bcj2.cc',
    ],
)
